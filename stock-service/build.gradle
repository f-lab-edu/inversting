plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.asciidoctor.jvm.convert'
    id 'jacoco'
}

group = "${projectGroup}"
version = "${applicationVersion}"
sourceCompatibility = "${javaVersion}"

ext {
    set('springCloudVersion', "2022.0.4")
    set('snippetsDir', file("build/generated-snippets"))
}

repositories {
    mavenCentral()
}

configurations {
    asciidoctorExtensions
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    implementation "org.springframework.cloud:spring-cloud-starter-openfeign:${openFeignVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation "io.github.resilience4j:resilience4j-spring-boot3:${resilience4jSpringBootVersion}"
    implementation "io.github.resilience4j:resilience4j-all:${resilience4jAllVersion}"
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    implementation "com.github.gavlyukovskiy:p6spy-spring-boot-starter:${p6spyVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"

    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo:${embededMongoVersion}"
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    finalizedBy 'asciidoctor'
}

// Querydsl 설정부
def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [ generated ]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(generated)
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExtensions'
    dependsOn test
}
